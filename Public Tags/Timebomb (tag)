{//; BLARGBOT TIMEBOMB COMMAND

* Created by k6ka#1014
* Code is released under a Creative Commons Attribution-ShareAlike license
* Give credit where credit is due!
* See https://github.com/CzarHey/discord-timebomb for instructions

Version 2.1T dated June 6, 2022
}
{//;Version number}
{void;
	{set;~version;2.1T}
	{set;~dated;June 6, 2022}
}

{//;Define debugging output, triggered with flag -d.}
{function;debug;
	{if;{flagset;d};==;true;{file;Report generated {time;YYYY/MM/DD HH:mm:ss}
Version number: {get;~version} dated {get;~dated}
Command name: {commandname}
Message sender: {messagesender} ({exec;person;{messagesender}})
Server: {guildid} ({guildname})
Is staff: {isstaff}
Is CC: {iscc}
User input: {args}

==Variables==

Banlists
~serverBanned: {get;~serverBanned}
~channelBanned: {get;~channelBanned}
~userBanned: {get;~userBanned}

Debug functionality
~canResetVariables: {get;~canResetVariables}
~approveToReset: {get;~approveToReset}
@timebomb-scores-success-guildid-flagt: {get;@timebomb-scores-success-{guildid}-{flag;t}}
@timebomb-scores-fail-guildid-flagt: {get;@timebomb-scores-fail-{guildid}-{flag;t}}
@timebomb-scores-timeout-guildid-flagt: {get;@timebomb-scores-timeout-{guildid}-{flag;t}}
@timebomb-scores-bombs-guildid-flagt: {get;@timebomb-scores-bombs-{guildid}-{flag;t}}
@timebomb-scores-streak-guildid-flagt: {get;@timebomb-scores-streak-{guildid}-{flag;t}}
@timebomb-scores-streakrecord-guildid-flagt: {get;@timebomb-scores-streakrecord-{guildid}-{flag;t}}

Bomb variables
~currentTimeMinusLastTime: {get;~currentTimeMinusLastTime}
~canBombUser: {get;~canBombUser}
@timebomb-lastBombTime-guildid-userid: {get;@timebomb-lastBombTime-{guildid}-{userid}}
~victim: {get;~victim}
~victimType: {get;~victimType}
~wireColor: {get;~wireColor}
~wireColorText: {get;~wireColorText}
~msgEmoji: {get;~msgEmoji}
~msgColor: {get;~msgColor}
~msgTitle: {get;~msgTitle}
~msgText: {get;~msgText}
~msgFooter: {get;~msgFooter}
~msgID: {get;~msgID}
~reaction: {get;~reaction}
~result: {get;~result}
@timebomb-scores-success-guildid-victim: {get;@timebomb-scores-success-{guildid}-{get;~victim}}
@timebomb-scores-fail-guildid-victim: {get;@timebomb-scores-fail-{guildid}-{get;~victim}}
@timebomb-scores-timeout-guildid-victim: {get;@timebomb-scores-timeout-{guildid}-{get;~victim}}
@timebomb-scores-bombs-guildid-userid: {get;@timebomb-scores-bombs-{guildid}-{userid}}
@timebomb-scores-streak-guildid-victim: {get;@timebomb-scores-streak-{guildid}-{get;~victim}}
@timebomb-scores-streakrecord-guildid-victim: {get;@timebomb-scores-streakrecord-{guildid}-{get;~victim}}
~timeLeftToBomb: {get;~timeLeftToBomb}
        
Scores variables
~scoresUser: {get;~scoresUser}
@timebomb-scores-success-guildid-scoresUser: {get;@timebomb-scores-success-{guildid}-{flag;t}}
@timebomb-scores-fail-guildid-scoresUser: {get;@timebomb-scores-fail-{guildid}-{flag;t}}
@timebomb-scores-timeout-guildid-scoresUser: {get;@timebomb-scores-timeout-{guildid}-{flag;t}}
@timebomb-scores-bombs-guildid-scoresUser: {get;@timebomb-scores-bombs-{guildid}-{flag;t}}
@timebomb-scores-streak-guildid-scoresUser: {get;@timebomb-scores-streak-{guildid}-{flag;t}}
@timebomb-scores-streakrecord-guildid-scoresUser: {get;@timebomb-scores-streakrecord-{guildid}-{flag;t}}
@timebomb-scores-success-guildid-total: {get;@timebomb-scores-success-{guildid}-total}
@timebomb-scores-fail-guildid-total: {get;@timebomb-scores-fail-{guildid}-total}
@timebomb-scores-timeout-guildid-total: {get;@timebomb-scores-timeout-{guildid}-total}
@timebomb-scores-bombs-guildid-total: {get;@timebomb-scores-bombs-{guildid}-total}
@timebomb-scores-streak-guildid-total: {get;@timebomb-scores-streak-{guildid}-total}
@timebomb-scores-streakrecord-guildid-total: {get;@timebomb-scores-streakrecord-{guildid}-total}
@timebomb-scores-success-global-total: {get;@timebomb-scores-success-global-total}
@timebomb-scores-fail-global-total: {get;@timebomb-scores-fail-global-total}
@timebomb-scores-timeout-global-total: {get;@timebomb-scores-timeout-global-total}
@timebomb-scores-bombs-global-total: {get;@timebomb-scores-bombs-global-total}
@timebomb-scores-streak-global-total: {get;@timebomb-scores-streak-global-total}
@timebomb-scores-streakrecord-global-total: {get;@timebomb-scores-streakrecord-global-total};timebomb_debug_output-{time;YYYYMMDDTHHmmss}.txt}
	}
}

{//;Scores output, generated by flag -s}
{if;{flagset;s};==;true;
	{//;Check user input.}
	{if;{length;{flag;s}};>;0;
		{set;~scoresUser;{suppresslookup}{userid;{flag;s}}}
		{if;{get;~scoresUser};==;`No user found`;{//;
			If user isn't found, display an error message
			}‚ùå  |  Oops! {usermention}, either I couldn't find the specified user in this server, or you cancelled the query! Here are some things you can try:{newline}{//;
			}‚Ä¢ Mention the user instead of typing out their username.{newline}{//;
			}‚Ä¢ Type out the full username, including the user tag.{newline}{//;
			}‚Ä¢ Use a user ID number instead of the username.{func.debug}
			;{//;Else, display the scores}
			{output;
				{embed;
					{embedbuild;
						title:üèÜ Timebomb Scores for {exec;person;{get;~scoresUser}};
						description:{//;
							}‚úÖ Number of successful defusals: {if;{get;@timebomb-scores-success-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-success-{guildid}-{get;~scoresUser}}}{newline}{//;
							}üí• Number of unsuccessful defusals: {if;{get;@timebomb-scores-fail-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-fail-{guildid}-{get;~scoresUser}}}{newline}{//;
							}‚è≤Ô∏è Number of timeouts: {if;{get;@timebomb-scores-timeout-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-timeout-{guildid}-{get;~scoresUser}}}{newline}{//;
							}üí£ Number of bombs thrown: {if;{get;@timebomb-scores-bombs-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-bombs-{guildid}-{get;~scoresUser}}}{newline}{//;
							}üå©Ô∏è Current winning streak: {if;{get;@timebomb-scores-streak-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-streak-{guildid}-{get;~scoresUser}}}{newline}{//;
							}üèÖ Highest streak: {if;{get;@timebomb-scores-streakrecord-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;@timebomb-scores-streakrecord-{guildid}-{get;~scoresUser}}}
                   	  ;
						color:{if;{exec;usercolor;{get;~scoresUser}};==;;F7F8F9;{exec;usercolor;{get;~scoresUser}}};
						thumbnail.url:{useravatar;{get;~scoresUser}};
						footer.icon_url:{useravatar};
						footer.text:This command was initiated by {exec;person;{userid}}
					}
				}
				{func.debug}
			}
		}
	;{//;If left blank, display server and global scores}
		{output;
			{embed;
				{embedbuild;
					title:üèÜ Timebomb Scores;
					description:**Timebomb Scores for {guildname}**{newline;2}{//;
						}‚úÖ Number of successful defusals: {if;{get;@timebomb-scores-success-{guildid}-total};==;{//;Empty};0;{get;@timebomb-scores-success-{guildid}-total}}{newline}{//;
						}üí• Number of unsuccessful defusals: {if;{get;@timebomb-scores-fail-{guildid}-total};==;{//;Empty};0;{get;@timebomb-scores-fail-{guildid}-total}}{newline}{//;
						}‚è≤Ô∏è Number of timeouts: {if;{get;@timebomb-scores-timeout-{guildid}-total};==;{//;Empty};0;{get;@timebomb-scores-timeout-{guildid}-total}}{newline}{//;
						}üí£ Number of bombs thrown: {if;{get;@timebomb-scores-bombs-{guildid}-total};==;{//;Empty};0;{get;@timebomb-scores-bombs-{guildid}-total}}{newline}{//;
						}üèÖ Highest streak: {if;{get;@timebomb-scores-streakrecord-{guildid}-total};==;{//;Empty};None yet!;{get;@timebomb-scores-streakrecord-{guildid}-total} by {exec;person;{get;@timebomb-scores-streakrecord-{guildid}-crown}}}{newline;2}{//;

						}**Global Timebomb Scores**{newline;2}{//;
						}‚úÖ Number of successful defusals: {if;{get;@timebomb-scores-success-global-total};==;{//;Empty};0;{get;@timebomb-scores-success-global-total}}{newline}{//;
						}üí• Number of unsuccessful defusals: {if;{get;@timebomb-scores-fail-global-total};==;{//;Empty};0;{get;@timebomb-scores-fail-global-total}}{newline}{//;
						}‚è≤Ô∏è Number of timeouts: {if;{get;@timebomb-scores-timeout-global-total};==;{//;Empty};0;{get;@timebomb-scores-timeout-global-total}}{newline}{//;
						}üí£ Number of bombs thrown: {if;{get;@timebomb-scores-bombs-global-total};==;{//;Empty};0;{get;@timebomb-scores-bombs-global-total}}{newline}{//;
						}üèÖ Highest streak: {if;{get;@timebomb-scores-streakrecord-global-total};==;{//;Empty};None yet!;{get;@timebomb-scores-streakrecord-global-total} by {exec;person;{get;@timebomb-scores-streakrecord-global-crown}}};
					color:{if;{exec;usercolor;{userid}};==;;F7F8F9;{exec;usercolor;{userid}}};
					footer.icon_url:{useravatar};
					footer.text:This command was initiated by {exec;person;{userid}};
					{if;{guildicon};==;null;{//;Do not use thumbnail.url if no guild icon is set, otherwise it will error out};thumbnail.url:{guildicon}{semi}}
				}
			}
			{func.debug}
		}
	}
	{return}{//;Stop the rest of the command from running}
}

{//;Check to see if this is being run as a custom command in a server}
{if;{iscc};{//;If true, run the rest of the command}

{//;Server banlist‚Äîdo not allow the command to be used in the listed servers.

Edit the timebomb-serverbanlist command for the list.
}
{set;~serverBanned;{exec;timebomb-serverbanlist}}
{if;{get;~serverBanned};
	{output;‚ùå  |  This command has been disabled in this server by {exec;person;249223024962830338}.{func.debug}}{return}
}

{//;Channel banlist‚Äîdo not allow the command to be used in the listed channels.

Edit the timebomb-channelbanlist command for the list.
}
{set;~channelBanned;{exec;timebomb-channelbanlist}}
{if;{get;~channelBanned};
	{output;‚ùå  |  This command has been disabled in this channel by {exec;person;249223024962830338}.{func.debug}}{return}
}

{//;User banlist‚Äîdo not allow the command to be used by the listed users.

Edit the timebomb-userbanlist command for the list.
}
{set;~userBanned;{exec;timebomb-userbanlist}}
{if;{get;~userBanned};
	{output;‚ùå  |  Sorry {usermention}, but you are not permitted to use this command. Contact {exec;person;249223024962830338} for details.{func.debug}}{return}
}

{//;Check to see if the user left the command blank}
{if;>;{argslength};0;

	{//;Reset @timebomb-lastBombTime variable if flag -r is set. For debugging purposes only.}
	{if;{flagset;r};==;true;
		{//;k6ka can reset variables regardless of permissions}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
			{//;Allow server staff to reset variables}
			{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
		}

		{if;{get;~canResetVariables};==;true;
			{//;Reset variables}
			{set;@timebomb-lastBombTime-{guildid}-{flag;r};}
			{output;‚úÖ  |  Done, {usermention}! The cooldown timer for {exec;person;{flag;r}} has been reset.{func.debug}};
			{output;‚ùå  |  Sorry {usermention}, but you don't have permission to do this!{func.debug}}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Reset user scores variables if flag -t is set. For debugging purposes only.}
	{if;{flagset;t};==;true;
		{//;k6ka can reset variables regardless of permissions}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
			{//;Allow server staff to reset scores}
			{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
		}

		{if;{get;~canResetVariables};==;true;
			{//;Reset scores}
			{set;@timebomb-scores-success-{guildid}-{flag;t};}
			{set;@timebomb-scores-fail-{guildid}-{flag;t};}
			{set;@timebomb-scores-timeout-{guildid}-{flag;t};}
			{set;@timebomb-scores-bombs-{guildid}-{flag;t};}
			{set;@timebomb-scores-streak-{guildid}-{flag;t};}
			{set;@timebomb-scores-streakrecord-{guildid}-{flag;t};}
			{output;‚úÖ  |  Done, {usermention}! The scores for {exec;person;{flag;t}} have been reset.{func.debug}};
			{output;‚ùå  |  Sorry {usermention}, but you don't have permission to do this!{func.debug}}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Reset global scores variables if flag -o is set. For debugging purposes only.}
	{if;{flagset;o};==;true;
		{//;Only k6ka can reset global scores}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true}
		}

		{if;{get;~canResetVariables};==;true;
			{send;{channelid};‚ö†Ô∏è  |  **WARNING:** You are about to reset the global scores. To protect against accidental use, please enter `I confirm this message` below exactly. This message will expire in 15 seconds.}
			{void;
				{waitmessage;{channelid};{userid};{set;~approveToReset;{bool;{messagetext};contains;I confirm this message}};15
				}
			}
			{if;{get;~approveToReset};==;true;

				{//;Reset scores}
				{set;@timebomb-scores-success-global-total;}
				{set;@timebomb-scores-fail-global-total;}
				{set;@timebomb-scores-timeout-global-total;}
				{set;@timebomb-scores-bombs-global-total;}
				{set;@timebomb-scores-streak-global-total;}
				{set;@timebomb-scores-streakrecord-global-total;}
				{set;@timebomb-scores-streakrecord-global-crown;}
				{output;‚úÖ  |  Done, {usermention}! Global scores have been reset.{func.debug}}
				;{output;‚ùå  |  Query cancelled.{func.debug}}
			};{output;‚ùå  |  Sorry {usermention}, but you don't have permission to do this!{func.debug}}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Reset server scores variables if flag -g is set. For debugging purposes only.}
	{if;{flagset;g};==;true;
		{//;k6ka can reset variables regardless of permissions}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
			{//;Allow server staff to reset scores}
			{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
		}

		{if;{get;~canResetVariables};==;true;
			{send;{channelid};‚ö†Ô∏è  |  **WARNING:** You are about to reset the scores for this server. To protect against accidental use, please enter `I confirm this message` below exactly. This message will expire in 15 seconds.}
			{void;
				{waitmessage;{channelid};{userid};{set;~approveToReset;{bool;{messagetext};contains;I confirm this message}};15
				}
			}
			{if;{get;~approveToReset};==;true;
				{//;Reset scores}
				{set;@timebomb-scores-success-{guildid}-total;}
				{set;@timebomb-scores-fail-{guildid}-total;}
				{set;@timebomb-scores-timeout-{guildid}-total;}
				{set;@timebomb-scores-bombs-{guildid}-total;}
				{set;@timebomb-scores-streak-{guildid}-total;}
				{set;@timebomb-scores-streakrecord-{guildid}-total;}
				{set;@timebomb-scores-streakrecord-{guildid}-crown;}
				{output;‚úÖ  |  Done, {usermention}! Scores for {guildname} have been reset.{func.debug}}
				;{output;‚ùå  |  Query cancelled.{func.debug}}
			};{output;‚ùå  |  Sorry {usermention}, but you don't have permission to do this!{func.debug}}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Help output, generated by flag -h}
	{if;{flagset;h};==;true;
		{output;
			{switch;{args;1};
				2;{//;Second page}
				{embed;
					{embedbuild;
					title:‚ùì Timebomb Back-end Help;
					description:**__Troubleshooting__**{newline;2}{//;
					}‚Ä¢ **"blargbot is throwing `No message found` errors."** Make sure the bot has the "Read Message History" permission on the channel.{newline}{//;
					}‚Ä¢ **"blargbot is throwing `This command was not authorized by server staff` errors."** Reauthorize the command as staff using the specified instructions.{newline}{//;
					}‚Ä¢ **"blargbot is throwing `I dont have permission to Add Reactions` errors."** Make sure the bot has the "Add Reaction" permission on the channel.{newline}{//;
					}‚Ä¢ **"Bombed user picks a wire, but no response from the bot."** If the user picks a wire while the bot is still setting up the message, their input will be ignored. They must pick another wire or remove their reaction and try again.{newline}{//;
					}‚Ä¢ **"Bomb timer supposedly expires but no response from bot."** The bot may have gone down while the command was active. The command will appear to have frozen and no scores will be saved.{newline}{//;
					}‚Ä¢ **"Command is very slow to execute and throws an `An internal server error has occurred` error before continuing."** There is an issue with the bot's database that prevents the cooldown timer and scores from working. Report the issue on the bot's support server.;
					color:e09040;
					footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version {get;~version}
					}
				}
				;{//;First page}
				{embed;
					{embedbuild;
					title:‚ùì Timebomb Back-end Help;
					description:**This help document is only for server staff.** If you are looking for general end-user assistance, type `b!{commandname}` without any arguments.{newline;2}{//;
					}**__Resetting cooldown timers__**{newline;2}{//;
					}Each user has a cooldown timer that limits them to throwing one bomb per minute. Resetting a cooldown timer allows the user to bomb again without having to wait for the timer to expire. To reset a user's cooldown timer, type `b!{commandname} -r <user ID>`.{newline;2}{//;
					}**__Resetting scores__**{newline;2}{//;
					}You can reset a user's scores or the scoreboard of a server. Resetting a server's scoreboard will not reset the scores of individual users. There is currently no way to reset everyone's scores on all servers all at once.{newline;2}{//;
					}‚Ä¢ To reset a user's score, type `b!{commandname} -t <user ID>`{newline}{//;
					}‚Ä¢ To reset the current server's scores, type `b!{commandname} -g`{newline;2}{//;
					}The global scoreboard can only be reset by {exec;person;249223024962830338}. This is done by typing `b!{commandname} -o`.{newline;2}{//;
					}**__Debug mode__**{newline;2}{//;
					}Blargbot's `b!cc debug` feature does not work properly for imported commands. To partly remedy this, there is a built-in debug output feature. To enable debug output, use flag `-d` anywhere in the command. The debug output will be provided when the command ends.{newline;2}{//;
					}**__GitHub__**{newline;2}{//;
					}You can access more help or view the source code of the command on [GitHub](https://github.com/CzarHey/discord-timebomb "https://github.com/CzarHey/discord-timebomb").{newline;2}{//;
					}*Type `b!{commandname} -h 2` to view information about troubleshooting.*;
					color:e09040;
					footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version {get;~version}
					}
				}
			}
			{func.debug}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Is staff check.}
	{if;{isstaff};==;false;‚ùå  |  This command was not authorized by server staff, or the person who authorized it is no longer staff. Please alert server staff and let them know that someone with these permissions (<https://discordapi.com/permissions.html#8254>) needs to do the following:

1. Delete this command with `b!cc delete {commandname}`
2. Reimport the command with `b!cc import timebomb`

If you need more help regarding bot permissions, you can ask on the bot's support server: https://discord.gg/015GVxZxI8rtlJgXF
You can also ask {exec;person;249223024962830338}, the author of this command. You can find him on the bot's support server.
	{func.debug}{return}
	}

	{//;Check to see if the user has ever bombed anyone in the past}
	{if;{length;{get;@timebomb-lastBombTime-{guildid}-{userid}}};>;0;
		{//;If true, take the current Unix time and subtract it from the last bomb time}
		{set;~currentTimeMinusLastTime;
			{math;-;{time;X};{get;@timebomb-lastBombTime-{guildid}-{userid}}}
		}
		{//;If last bomb time was <= 60 seconds ago, do not allow user to bomb. If last bomb time was > 60 seconds ago, allow user to bomb.}
    	{if;{get;~currentTimeMinusLastTime};>;60;{set;~canBombUser;true};{set;~canBombUser;false}};

		{//;If user has never bombed anyone on this server, ever, allow them to bomb}
		{set;~canBombUser;true}
	}

	{//;Check to see if the bomber has bombed someone in the past 60 seconds based on the check above}
	{if;{get;~canBombUser};{//;If yes, continue command execution}

		{//;Get the victim's user ID and save it for later}
		{suppresslookup}{set;~victim;{userid;{args}}}
		{//;Validate input‚Äîif bomber cancels the query {userid} will return nothing}
		{if;{get;~victim};!=;`No user found`;
			{//;If ~victim isn't empty, continue}
			{//;Get further validation of input‚Äîmake sure user isn't trying to bomb blargbot, another bot, or themselves.}
			{set;~victimType;
				{//;Trying to blow up blargy?}{if;{get;~victim};==;134133271750639616;blargy;
					{//;Trying to blow up another bot?}{if;{userisbot;{get;~victim}};bot;
						{//;Trying to blow up themselves?}{if;{get;~victim};==;{userid};self;
							{//;Trying to blow up a user not in server?}{if;{exec;usercheck;{get;~victim}};==;false;absent;
								{//;If all checks passed, output valid}valid
							}
								
						}
					}
				}
			}

			{//;Process those results}
			{switch;{get;~victimType};
				blargy;‚ùå  |  You thought you could trick me into bombing myself, {usermention}?!;
				bot;‚ùå  |  I'm not bombing one of my own kin, {usermention}! Pick a *human* to blow up.;
				self;‚ùå  |  Please, {usermention}! Pick a friend if you have to!;
				absent;‚ùå  |  That user isn't on this server, {usermention}!;
				valid;{//;If ~victimType is valid, continue ‚Äì the user input is valid and we can bomb them}
				
				{//;Set the Unix timestamp of the bombing message for the cooldown timer. Use ! before variable to save to database immediately; this prevents users from bombing again right away.}
				{set;!@timebomb-lastBombTime-{guildid}-{userid};
					{messagetime;
						{messageid};X
					}
				}

				{//;Set the wire colour}
				{set;~wireColor;{randchoose; üî¥ ; ‚ö™ ; üîµ }}
              
				{//;Get the text version of the color}
				{set;~wireColorText;
					{switch;{get;~wireColor};
						üî¥ ; red;
						‚ö™ ; white;
						üîµ ; blue;
						‚ùå **FATAL ERROR**: No wire color was set!!!
					}
				}

				{//;Generate the message output text that the user is going to see}
				{void;
					{set;~msgEmoji;üí£}
					{set;~msgColor;e09040}
					{set;~msgTitle;{randchoose;
						Bombs Ahoy!;
						Fire in the hole!;
						I hear a fuse!;
						Look out behind you!;
						What's that ticking sound?;
						Code Red: Bomb Threat!!!
					}}
					{set;~msgText;
						{randchoose;
							„Å≠, {usermention;{get;~victim}}, there's a stowaway in your pants! It's a bomb on a one minute timer. There are three wires: red, white, and blue. Click on the colour emoji below to cut that wire and try to defuse the bomb!;
							Hey {usermention;{get;~victim}}, someone shoved a bomb down your pants! There's one minute to go on the fuse, and there are three wires: red, white, and blue. Quick, click on the colour emoji below to tell me which wire to cut!;
							Watch out, {usermention;{get;~victim}}! There's a bomb strapped to your undies! The timer is set for one minute, and there are three wires: red, white, and blue. Click on the colour emoji below to cut that wire.;
							*Achtung!* There's a bomb down your pants, {usermention;{get;~victim}}! There's a one minute fuse on it, and there appears to be three wires: red, white, and blue. Given where it is, you should let me cut one of the wires for you{semi} click on the colour emoji below to tell me which one to cut!;
							Hey {usermention;{get;~victim}}, I saw someone put a bomb down your pants. One minute fuse, three wires: red, white, and blue. Time is running out! Click on the colour emoji below to cut that wire.
						}
					}
					{set;~msgFooter;{exec;person;{userid}} bombed you, by the way.}
				}

				{//;Sends the first message}
				{//;Collect the ID of the message the bot sends, and suppresses msgID output from {send}}
				{set;~msgID;
					{send;{channelid};
						{usermention;{get;~victim}};
						{embedbuild;
							title:{get;~msgEmoji} {get;~msgTitle};
							description:{get;~msgText};
							color:{get;~msgColor};
							footer.icon_url:{useravatar};
							footer.text:{get;~msgFooter}
						}
					}
				}

				{//;Add the reactions to the message}
				{reactadd;{get;~msgID}; üî¥ ; ‚ö™ ; üîµ }

				{//;DMs the initiating user the colour of the correct wire}
				{dm;{userid};ü§´  |  Hey, don't tell {usernick;{get;~victim}}, but it's the {get;~wireColorText} wire.}

				{//; Wait until user reacts one of the emojis provided}
				{void;
					{waitreaction;{get;~msgID};{userid;{get;~victim}};üî¥ ‚ö™ üîµ ;{set;~reaction;{reaction}}true;60}
				}

				{//;Process the reaction. If they picked the right colour, return success. If not, return fail. If they timed out, return timeout.}
				{void;
					{set;~result;
						{switch;{get;~reaction};
							üî¥ ; {//;User reacted red}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
							‚ö™ ; {//;User reacted white}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
							üîµ ; {//;User reacted blue}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
								{//;User timed out}timeout
						}
					}
				}

				{//;Display either a success, fail, or timeout message}
				{switch;{get;~result};
					success;
						{//;Modify variables}
						{set;~msgEmoji;‚úÖ}
						{set;~msgColor;85b061}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{randchoose;
									Success, {usernick;{get;~victim}}! That was the correct wire! You have defused the bomb!;
									You did it, {usernick;{get;~victim}}! I'll be honest, I thought you were dead, but you managed to pick the right colour and saved yourself! Nicely done.;
									Phew! Nice work, {usernick;{get;~victim}}! You defused the bomb successfully!;
									Hey, the ticking sound's gone. Great job, {usernick;{get;~victim}}! You picked the right wire and saved yourself!
								};
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						}
						{//;Set scores}
						{void;
							{//;If the variables are empty (null), fallback to 1 to count first score. This fallback shouldn't apply to things outside of this void tag.}
							{fallback;1}
							{//;Set user score}
							{set;@timebomb-scores-success-{guildid}-{get;~victim};{math;+;{get;@timebomb-scores-success-{guildid}-{get;~victim}};1}}
							{//;Set server score}
							{set;@timebomb-scores-success-{guildid}-total;{math;+;{get;@timebomb-scores-success-{guildid}-total};1}}
							{//;Set global score}
							{set;@timebomb-scores-success-global-total;{math;+;{get;@timebomb-scores-success-global-total};1}}
							{//;Add to user streak score}
							{set;@timebomb-scores-streak-{guildid}-{get;~victim};{math;+;{get;@timebomb-scores-streak-{guildid}-{get;~victim}};1}}

							{//;Add to user streak record}
							{//;Check if streakrecord is null. It will be null if the user has never had a record.}
							{if;{get;@timebomb-scores-streakrecord-{guildid}-{get;~victim}};==;;
								{//;If null, set the record to one}{set;@timebomb-scores-streakrecord-{guildid}-{get;~victim};1};
								{//;If not null, check to see if the current streak is higher than the streakrecord}{if;{get;@timebomb-scores-streak-{guildid}-{get;~victim}};>;{get;@timebomb-scores-streakrecord-{guildid}-{get;~victim}};
									{//;If it's higher, set streakrecord to match score-streak}{set;@timebomb-scores-streakrecord-{guildid}-{get;~victim};{get;@timebomb-scores-streak-{guildid}-{get;~victim}}}
								}
							}

							{//;Add to server streak record}
							{//;Check if streakrecord is null. It will be null if the user has never had a record.}
							{if;{get;@timebomb-scores-streakrecord-{guildid}-total};==;;
								{//;If null, set the record to one, and give the crown to that user}{set;@timebomb-scores-streakrecord-{guildid}-total;1}{set;@timebomb-scores-streakrecord-{guildid}-crown;{get;~victim}};
								{//;If not null, check to see if the current streak is higher than the streakrecord}{if;{get;@timebomb-scores-streak-{guildid}-{get;~victim}};>;{get;@timebomb-scores-streakrecord-{guildid}-total};
									{//;If it's higher, set streakrecord to match score-streak}{set;@timebomb-scores-streakrecord-{guildid}-total;{get;@timebomb-scores-streak-{guildid}-{get;~victim}}}
									{//;Note down the victim's user ID as they are the new crown-holder}{set;@timebomb-scores-streakrecord-{guildid}-crown;{get;~victim}}
								}
							}

							{//;Add to global streak record}
							{//;Check if streakrecord is null. It will be null if the user has never had a record.}
							{if;{get;@timebomb-scores-streakrecord-global-total};==;;
								{//;If null, set the record to one, and give the crown to that user}{set;@timebomb-scores-streakrecord-global-total;1}{set;@timebomb-scores-streakrecord-global-crown;{get;~victim}};
								{//;If not null, check to see if the current streak is higher than the streakrecord}{if;{get;@timebomb-scores-streak-{guildid}-{get;~victim}};>;{get;@timebomb-scores-streakrecord-global-total};
									{//;If it's higher, set streakrecord to match score-streak}{set;@timebomb-scores-streakrecord-global-total;{get;@timebomb-scores-streak-{guildid}-{get;~victim}}}
									{//;Note down the victim's user ID as they are the new crown-holder}{set;@timebomb-scores-streakrecord-global-crown;{get;~victim}}
								}
							}
						};
					fail;
						{//;Modify variables}
						{set;~msgEmoji;üí•}
						{set;~msgColor;ac2d3a}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{randchoose;
									Nope, wrong wire, {usernick;{get;~victim}}! Aww, now you've gone and killed yourself. Wow. Sorry.;
									Whoops, looks like that wasn't the correct wire, {usernick;{get;~victim}}! Well, now you're dead. Sorry about that.;
									Oops! That was the wrong wire, {usernick;{get;~victim}}! Oh great, now the bomb has gone off. RIP.;
									The bomb went off and blew {usernick;{get;~victim}} to smithereens! Oh well, I'm sure they'll have better luck next time.;
									Uh oh, the bomb's going off! Take cover!!! ...wait, {usernick;{get;~victim}}, the bomb is on YOU! Oh well, I guess you're going to die anyway.;
									Aww, tough luck, {usernick;{get;~victim}}. Well, I guess I have to clean up all of the guts in this place.
								} (You should have picked the {get;~wireColorText} wire);
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						}
						{//;Set scores}
						{void;
							{//;If the variables are empty (null), fallback to 1 to count first score. This fallback shouldn't apply to things outside of this void tag.}
							{fallback;1}
							{//;Set user score}
							{set;@timebomb-scores-fail-{guildid}-{get;~victim};{math;+;{get;@timebomb-scores-fail-{guildid}-{get;~victim}};1}}
							{//;Set server score}
							{set;@timebomb-scores-fail-{guildid}-total;{math;+;{get;@timebomb-scores-fail-{guildid}-total};1}}
							{//;Set global score}
							{set;@timebomb-scores-fail-global-total;{math;+;{get;@timebomb-scores-fail-global-total};1}}
							{//;Reset user streak score}
							{set;@timebomb-scores-streak-{guildid}-{get;~victim};}
						};
					timeout;
						{//;Modify variables}
						{set;~msgEmoji;üí•}
						{set;~msgColor;ac2d3a}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{randchoose;
									Oh come on, {usernick;{get;~victim}}, you could've at least picked something! Well, now you're dead. You see that? Guts everywhere.;
									Too slow, {usernick;{get;~victim}}! You were a sitting duck waiting for the bomb to go off. Try to save yourself next time!;
									Well, it looks like {usernick;{get;~victim}} failed to pick a wire in time. Their indecisveness sure paid off. With their death!;
									Please, {usernick;{get;~victim}}, you could've at least tried! Oh well. Rest in pieces, I guess.;
									Hello? Earth to {usernick;{get;~victim}}? You were just bombed! Next time, you should tell me which wire to cut to try to save yourself from annihilation.
								} (You should have picked the {get;~wireColorText} wire);
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						}
						{//;Set scores}
						{void;
							{//;If the variables are empty (null), fallback to 1 to count first score. This fallback shouldn't apply to things outside of this void tag.}
							{fallback;1}
							{//;Set user score}
							{set;@timebomb-scores-timeout-{guildid}-{get;~victim};{math;+;{get;@timebomb-scores-timeout-{guildid}-{get;~victim}};1}}
							{//;Set server score}
							{set;@timebomb-scores-timeout-{guildid}-total;{math;+;{get;@timebomb-scores-timeout-{guildid}-total};1}}
							{//;Set global score}
							{set;@timebomb-scores-timeout-global-total;{math;+;{get;@timebomb-scores-timeout-global-total};1}}
							{//;Reset streak score}
							{set;@timebomb-scores-streak-{guildid}-{get;~victim};}
						};
					‚ùå  |  Uh oh, something went wrong! Please report this error to {exec;person;249223024962830338}!
				}

				{void;
					{//;If the variables are empty (null), fallback to 1 to count first score. This fallback shouldn't apply to things outside of this void tag.}
					{fallback;1}
					{//;Set the bomber's bombs scores}
					{set;@timebomb-scores-bombs-{guildid}-{userid};{math;+;{get;@timebomb-scores-bombs-{guildid}-{userid}};1}}
					{//;Set the server's bombs scores}
					{set;@timebomb-scores-bombs-{guildid}-total;{math;+;{get;@timebomb-scores-bombs-{guildid}-total};1}}
					{//;Set the global bombs scores}
					{set;@timebomb-scores-bombs-global-total;{math;+;{get;@timebomb-scores-bombs-global-total};1}}
				}
			}

			;{//;If ~victim is empty}
			‚ùå  |  Oops! {usermention}, either I couldn't find the specified user in this server, or you cancelled the query! Here are some things you can try:{newline}{//;
			}‚Ä¢ Mention the user instead of typing out their username.{newline}{//;
			}‚Ä¢ Type out the full username, including the user tag.{newline}{//;
			}‚Ä¢ Use a user ID number instead of the username.
		}


	;{//;If ~canBombUser is false, show this message instead}{set;~timeLeftToBomb;{math;-;60;{get;~currentTimeMinusLastTime}}}‚ùå  |  Hey {usermention}, it looks like you've already bombed someone in the past minute. Please wait {if;{get;~timeLeftToBomb};>;1;another {get;~timeLeftToBomb} seconds;just a few more seconds} before trying again.
	}

;{//;If command was left empty}
{embed;
	{embedbuild;
		title:üí£ Tick, tick... BOOM!;
		description:A recreation of an IRC classic: timebombs! Get your friends and family together for some good 'ol explosive fun!;
		fields.name:How to Bomb;
		fields.value:Type `b!{commandname} <username>`, replacing `<username>` with the username of the user you want to bomb. You can also mention them, or use their user ID.{newline;2}{//;
		}The bombed user will be pinged and presented with three wires‚Äîred, white, and blue‚Äîin the form of emoji reactions. They need to select one of the three in order to attempt to defuse the bomb. If they pick the right colour, they will be saved{semi} if they pick the wrong one, or don't pick one in time, they will be blown up.;
		fields.name:Keeping Score;
		fields.value:The command keeps track of all of the bombs you've thrown, and the number of times you defused or were blown up by a bomb. It also keeps track of your highest winning streak.{newline}{//;
		}‚Ä¢ To view the local and global scoreboard, type `b!{commandname} -s`{newline}{//;
		}‚Ä¢ To view a user's scores, type `b!{commandname} -s <username>`;
		fields.name:Need help?;
		fields.value:Type `b!{commandname} -h` to view additional documentation for the command. You can also find more documentation on [GitHub](https://github.com/CzarHey/discord-timebomb "https://github.com/CzarHey/discord-timebomb").{newline;2}{//;
		}If you have any questions, or if you found a bug with the command, join the [blargbot support server](https://discord.gg/015GVxZxI8rtlJgXF "https://discord.gg/015GVxZxI8rtlJgXF") and find me, {exec;person;249223024962830338}, there. I'll do my best to help you out.;
		color:e09040;
		footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version {get;~version}
	}
}

}{//;End blank input check}

;{//;If not being run as a CC, will output this instead}
{embed;
	{embedbuild;
	title:üí£ Timebomb Command for Discord;
	description:The "timebomb" command was a fun command found on several IRC bots that provided a light, entertaining form of amusement and user engagement. It worked a little like this:{newline;2}{//;
		}‚Ä¢ User A uses the "timebomb" command on User B.{newline}{//;
		}‚Ä¢ The IRC bot will ping User B and present them with a selection of coloured wires to cut, along with how much time is on the fuse.{newline}{//;
		}‚Ä¢ User B needs to respond back with the correct colour in order to defuse the bomb.{newline}{//;
		}‚Ä¢ If User B guesses the right colour, they are saved.{newline}{//;
		}‚Ä¢ If User B guesses the wrong colour, or if they don't respond by the time the timer runs out, the bomb will go off.{newline;2}{//;
		}This is a recreation of the classic IRC timebomb command for Discord, created using blargbot's BBTag programming language.{newline;2}{//;
		}**Cool! How do I add this to my server?**{newline;2}{//;
		}Since this command needs the `send` tag, which is disabled on the public tags system, you'll need to import this command into your server as a custom command in order for it to work. Before you begin, double-check the following:{newline;2}{//;
		}‚Ä¢ You are either the Owner of, or have the Admin permission on, the server you want to add it to.{newline}{//;
		}‚Ä¢ blargbot has permissions to Read and Send Messages, Add Emoji Reactions, and Read Message History.{newline;2}{//;
		}You can then import the command to your server by typing `b!cc import timebomb` in any channel the bot has access to. The command will automatically be added to your server, and can be used by typing `b!timebomb`. If desired, you may rename the command using the `b!cc rename` command.{newline;2}{//;
		}If you would like more customization, or if you would like to view the source code of the command, please see the [GitHub repository](https://github.com/CzarHey/discord-timebomb "https://github.com/CzarHey/discord-timebomb").{newline;2}{//;
		}I highly encourage you to give this a go and invite some of your friends over to play around with it. Relive some classic IRC fun right in the modern comforts of Discord!;
	color:e09040;
	footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version {get;~version}
	}
}
}{//;End CC check}
{func.debug}
