{//; BLARGBOT TIMEBOMB COMMAND

* Created by k6ka#1014
* Code is released under a Creative Commons Attribution-ShareAlike license
* Give credit where credit is due!
* See https://k6ka.blogspot.com/2020/05/timebomb-command-for-discord.html for instructions

Version 2.0TD dated September 26, 2020
}
{//;Version number}
{void;
	{set;~version;2.0TD}
}
{//;Check to see if this is being run as a custom command in a server}
{if;{iscc};{//;If true, run the rest of the command}

{//;Server blacklist—do not allow the command to be used in the following servers.

Format is:
	<server ID> <name of server in comments>; true

}
{set;~serverBanned;
	{switch;{guildid};
		guildID;true
		;false
	}
}
{if;{get;~serverBanned};
	{output;❌  |  This command has been disabled in this server by {exec;person;249223024962830338}.}{return}
}

{//;Check to see if the user left the command blank}
{if;>;{argslength};0;

	{//;Reset lastBombTime variable if flag -r is set. For debugging purposes only.}
	{if;{flagset;r};==;true;
		{//;k6ka can reset variables regardless of permissions}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
			{//;Allow server staff to reset variables}
			{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
		}

		{if;{get;~canResetVariables};==;true;
			{//;Reset variables}
			{set;lastBombTime-{guildid}-{flag;r};}
			{output;✅  |  Done, {usermention}! The cooldown timer for {exec;person;{flag;r}} has been reset.};
			{output;❌  |  Sorry {usermention}, but you don't have permission to do this!}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Reset scores variables if flag -t is set. For debugging purposes only.}
	{if;{flagset;t};==;true;
		{//;k6ka can reset variables regardless of permissions}
		{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
			{//;Allow server staff to reset scores}
			{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
		}

		{if;{get;~canResetVariables};==;true;
			{//;Reset scores}
			{set;scores-success-{guildid}-{flag;t};}
			{set;scores-fail-{guildid}-{flag;t};}
			{set;scores-timeout-{guildid}-{flag;t};}
			{set;scores-bombs-{guildid}-{flag;t};}
			{set;scores-streak-{guildid}-{flag;t};}
			{output;✅  |  Done, {usermention}! The scores for {exec;person;{flag;t}} have been reset.};
			{output;❌  |  Sorry {usermention}, but you don't have permission to do this!}
		}
		{return}{//;Stop the rest of the command from running}
	}
  
	{//;Scores output, generated by flag -s}
	{if;{flagset;s};==;true;
		{//;Check user input.}
		{set;~scoresUser;{userid;{flag;s};quiet}}
		{//;If user specified isn't found, default to current user.}
		{if;{get;~scoresUser};==;;{set;~scoresUser;{userid}};{//;Don't change ~scoresUser; keep it the same}}

		{output;
			{embed;
				{embedbuild;
					title:🏆 Timebomb Scores for {exec;person;{get;~scoresUser}};
					description:{//;
						}✅ Number of successful defusals: {if;{get;scores-success-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;scores-success-{guildid}-{get;~scoresUser}}}{newline}{//;
						}💥 Number of unsuccessful defusals: {if;{get;scores-fail-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;scores-fail-{guildid}-{get;~scoresUser}}}{newline}{//;
						}⏲️ Number of timeouts: {if;{get;scores-timeout-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;scores-timeout-{guildid}-{get;~scoresUser}}}{newline}{//;
						}💣 Number of bombs thrown: {if;{get;scores-bombs-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;scores-bombs-{guildid}-{get;~scoresUser}}}{newline}{//;
						}🌩️ Current winning streak: {if;{get;scores-streak-{guildid}-{get;~scoresUser}};==;{//;Empty};0;{get;scores-streak-{guildid}-{get;~scoresUser}}};
					color:{if;{exec;usercolor;{get;~scoresUser}};==;;F7F8F9;{exec;usercolor;{get;~scoresUser}}};
					footer.text:Initiated by {exec;person;{userid}}
				}
			}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Help output, generated by flag -h}
	{if;{flagset;h};==;true;
		{output;
			{embed;
				{embedbuild;
				title:💣 Timebomb Help;
				description:**__Using the command__**{newline;2}{//;
				}Type ``b!{commandname} <username / user ID / user mention>`` to bomb the selected user.{newline}{//;
				}The bot will respond by mentioning the target user and adding three emoji reactions to the message: a red, white, and blue circle.{newline}{//;
				}The bot will then DM you the correct wire colour, just for fun!{newline;2}{//;
				}**__Cutting the wire__**{newline;2}{//;
				}Click on one of the coloured emoji reactions the bot presents to you to cut the wire. Only the bombed user can cut the wire{semi} other users cannot cut it for them.{newline;2}{//;
				}**__Outcome__**{newline;2}{//;
				}• If the target user cuts the correct wire, they will be saved. If not, the bomb will go off.{newline}{//;
				}• If the target user doesn't respond within one minute, the bomb will go off.{newline}{//;
				}• If the bomb goes off, the target user will be informed of the correct wire colour that they should've picked.{newline;2}{//;
				}**__Can't bomb someone?__**{newline;2}{//;
				}You cannot bomb other bots, nor can you bomb yourself. You also cannot bomb if you have already bombed someone within the past minute.{newline;2}{//;
				}**__Flags__**{newline;2}{//;
				}``-d`` – Enables debug output. For debugging purposes only.{newline}{//;
				}``-h`` – Displays this help output.{newline}{//;
				}``-q`` – Quiet mode. Silences the DM the bot sends to the bombing user.{newline}{//;
				}``-r`` – Resets the variables for the command. For debugging purposes only.{newline;2}{//;
				}**__Found a bug?__**{newline;2}{//;
				}Join the [blargbot support server](https://discord.gg/015GVxZxI8rtlJgXF "https://discord.gg/015GVxZxI8rtlJgXF") and find me, {exec;person;249223024962830338}, there. I'll do my best to help you out.;
				color:e09040;
				footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license • Version {get;~version}
				}
			}
		}
		{return}{//;Stop the rest of the command from running}
	}

	{//;Check to see if the user has ever bombed anyone in the past}
	{if;{length;{get;lastBombTime-{guildid}-{userid}}};>;0;
		{//;If true, take the current Unix time and subtract it from the last bomb time}
		{set;~currentTimeMinusLastTime;
			{math;-;{time;X};{get;lastBombTime-{guildid}-{userid}}}
		}
		{//;If last bomb time was <= 60 seconds ago, do not allow user to bomb. If last bomb time was > 60 seconds ago, allow user to bomb.}
    	{if;{get;~currentTimeMinusLastTime};>;60;{set;~canBombUser;true};{set;~canBombUser;false}};

		{//;If user has never bombed anyone on this server, ever, allow them to bomb}
		{set;~canBombUser;true}
	}

	{//;Check to see if the bomber has bombed someone in the past 60 seconds based on the check above}
	{if;{get;~canBombUser};{//;If yes, continue command execution}

		{//;Get the victim's user ID and save it for later}
		{set;~victim;{userid;{args}}}
		{//;Validate input—if bomber cancels the query {userid} will return nothing}
		{if;{length;{get;~victim}};>;0;
			{//;If ~victim isn't empty, continue}
			{//;Get further validation of input—make sure user isn't trying to bomb blargbot, another bot, or themselves.}
			{set;~victimType;
				{//;Trying to blow up blargy?}{if;{get;~victim};==;134133271750639616;blargy;
					{//;Trying to blow up another bot?}{if;{userisbot;{get;~victim}};bot;
						{//;Trying to blow up themselves?}{if;{get;~victim};==;{userid};self;
							{//;If all checks passed, output valid}valid
						}
					}
				}
			}

			{//;Process those results}
			{switch;{get;~victimType};
				blargy;❌  |  You thought you could trick me into bombing myself, {usermention}?!;
				bot;❌  |  I'm not bombing one of my own kin, {usermention}! Pick a *human* to blow up.;
				self;❌  |  Please, {usermention}! Pick a friend if you have to!;
				valid;{//;If ~victimType is valid, continue – the user input is valid and we can bomb them}
				
				{//;Set the Unix timestamp of the bombing message for the cooldown timer. Use ! before variable to save to database immediately; this prevents users from bombing again right away.}
				{set;!lastBombTime-{guildid}-{userid};
					{messagetime;
						{messageid};X
					}
				}

				{//;Set the wire colour}
				{set;~wireColor;{randchoose; 🔴 ; ⚪ ; 🔵 }}
              
				{//;Get the text version of the color}
				{set;~wireColorText;
					{switch;{get;~wireColor};
						🔴 ; red;
						⚪ ; white;
						🔵 ; blue;
						❌ **FATAL ERROR**: No wire color was set!!!
					}
				}

				{//;Generate the message output text that the user is going to see}
				{void;
					{set;~msgEmoji;💣}
					{set;~msgColor;e09040}
					{set;~msgTitle;Bombs Ahoy!}
					{set;~msgText;
						{randchoose;
							ね, {usermention;{get;~victim}}, there's a stowaway in your pants! It's a bomb on a one minute timer. There are three wires: red, white, and blue. Click on the colour emoji below to cut that wire and try to defuse the bomb!;
							Hey {usermention;{get;~victim}}, someone shoved a bomb down your pants! There's one minute to go on the fuse, and there are three wires: red, white, and blue. Quick, click on the colour emoji below to tell me which wire to cut!;
							Watch out, {usermention;{get;~victim}}! There's a bomb strapped to your undies! The timer is set for one minute, and there are three wires: red, white, and blue. Click on the colour emoji below to cut that wire.;
							*Achtung!* There's a bomb down your pants, {usermention;{get;~victim}}! There's a one minute fuse on it, and there appears to be three wires: red, white, and blue. Given where it is, you should let me cut one of the wires for you{semi} click on the colour emoji below to tell me which one to cut!;
							Hey {usermention;{get;~victim}}, I saw someone put a bomb down your pants. One minute fuse, three wires: red, white, and blue. Time is running out! Click on the colour emoji below to cut that wire.
						}
					}
					{set;~msgFooter;{exec;person;{userid}} bombed you, by the way.}
				}

				{//;Sends the first message}
				{//;Collect the ID of the message the bot sends, and suppresses msgID output from {send}}
				{set;~msgID;
					{send;{channelid};
						{usermention;{get;~victim}};
						{embedbuild;
							title:{get;~msgEmoji} {get;~msgTitle};
							description:{get;~msgText};
							color:{get;~msgColor};
							footer.icon_url:{useravatar};
							footer.text:{get;~msgFooter}
						}
					}
				}

				{//;Add the reactions to the message}
				{reactadd;{get;~msgID}; 🔴 ; ⚪ ; 🔵 }

				{//;DMs the initiating user the colour of the correct wire}
				{dm;{userid};🤫  |  Hey, don't tell {usernick;{get;~victim}}, but it's the {get;~wireColorText} wire.}

				{//; Wait until user reacts one of the emojis provided}
				{void;
					{waitreaction;{get;~msgID};{userid;{get;~victim}};🔴 ⚪ 🔵 ;{set;~reaction;{reaction}}true;60}
				}

				{//;Process the reaction. If they picked the right colour, return success. If not, return fail. If they timed out, return timeout.}
				{void;
					{set;~result;
						{switch;{get;~reaction};
							🔴 ; {//;User reacted red}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
							⚪ ; {//;User reacted white}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
							🔵 ; {//;User reacted blue}{if;{get;~reaction};contains;{get;~wireColor};success;fail};
								{//;User timed out}timeout
						}
					}
				}

				{//;Display either a success, fail, or timeout message}
				{switch;{get;~result};
					success;
						{//;Modify variables}
						{set;~msgEmoji;✅}
						{set;~msgColor;85b061}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{exec;bomb-successmsg};
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						};
					fail;
						{//;Modify variables}
						{set;~msgEmoji;💥}
						{set;~msgColor;ac2d3a}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{exec;bomb-failmsg} (You should have picked the {get;~wireColorText} wire);
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						};
					timeout;
						{//;Modify variables}
						{set;~msgEmoji;💥}
						{set;~msgColor;ac2d3a}
						{//;Edit the message}
						{edit;{get;~msgID};;
							{embedbuild;
								title:{get;~msgEmoji} {get;~msgTitle};
								description:{get;~msgText}{newline;2}{exec;bomb-timeoutmsg} (You should have picked the {get;~wireColorText} wire);
								color:{get;~msgColor};
								footer.icon_url:{useravatar};
								footer.text:{get;~msgFooter}
							}
						};
					❌  |  Uh oh, something went wrong! Please report this error to {exec;person;249223024962830338}!
				}
			}

			;{//;If ~victim is empty}
			❌  |  Oops! {usermention}, either I couldn't find the specified user in this server, or you cancelled the query! Here are some things you can try:{newline}{//;
			}• Mention the user instead of typing out their username.{newline}{//;
			}• Type out the full username, including the user tag.{newline}{//;
			}• Use a user ID number instead of the username.
		}
		

	;{//;If ~canBombUser is false, show this message instead}{set;~timeLeftToBomb;{math;-;60;{get;~currentTimeMinusLastTime}}}❌  |  Hey {usermention}, it looks like you've already bombed someone in the past minute. Please wait {if;{get;~timeLeftToBomb};>;1;another {get;~timeLeftToBomb} seconds;just a few more seconds} before trying again.
	}

;{//;If command was left empty}
Nothing.

}

;{//;If not being run as a CC, will output this instead}
{embed;
	{embedbuild;
	title:💣 Tick, tick... BOOM!;
	description:Many IRC bots have a "timebomb" fun command that provides endless hours of ensuing fun with other users. It works a little like this:{newline;2}{//;
    }• User A uses the "timebomb" command on User B.{newline}{//;
    }• The IRC bot will ping User B and present them with a selection of coloured wires to cut, along with how much time is on the fuse.{newline}{//;
    }• User B needs to respond back with the correct colour in order to defuse the bomb.{newline}{//;
    }• If User B guesses the right colour, they are saved.{newline}{//;
    }• If User B guesses the wrong colour, or if they don't respond by the time the timer runs out, the bomb will go off.{newline;2}{//;
    }I couldn't find any equivalent functionality on Discord, so I made my own implementation using blargbot's BBTag language. It works pretty well, and of course, I'm making the code for it open to everyone.{newline;2}{//;
    }**Cool! How do I add this to my server?**{newline;2}{//;
    }Since this command needs the ``send`` tag, which is disabled on the public tags system, you'll need to import this command into your server as a custom command in order for it to work. Before you begin, double-check the following:{newline;2}{//;
    }• You are either the Owner of, or have Admin permissions on, the server you want to add it to.{newline}{//;
    }• blargbot has permissions to Read and Send Messages, Add Emoji Reactions, and Read Message History.{newline;2}{//;
    }You can then import the command in two ways:{newline;2}{//;
    }1. Import this command to your server by typing ``b!cc import timebomb``. It will be available for use right away, with no additional setup, and you'll get the latest updates to the command automatically.{newline}{//;
    }2. Import the command manually, which will give you more control over the command and allow you to customize it to your liking. Instructions can be found on [my blog](https://k6ka.blogspot.com/2020/05/timebomb-command-for-discord.html "https://k6ka.blogspot.com/2020/05/timebomb-command-for-discord.html").{newline;2}{//;
    }I highly encourage you to give this a go and invite some of your friends over to play around with it. Relive some classic IRC fun right in the modern comforts of Discord!;
	color:e09040;
	footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license • Version 1.5T
	}
}
}
