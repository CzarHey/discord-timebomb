{//; BLARGBOT TIMEBOMB COMMAND

* Created by k6ka#1014
* Code is released under a Creative Commons Attribution-ShareAlike license
* Give credit where credit is due!
* See https://k6ka.blogspot.com/2020/05/timebomb-command-for-discord.html for instructions

Version 1.3 dated August 7, 2020
}

{//;Reset lastBombTime variable if flag -r is set. For debugging purposes only.}
{if;{flagset;r};==;true;
	{//;k6ka can reset variables regardless of permissions}
	{if;{userid};==;249223024962830338;{set;~canResetVariables;true};
		{//;Allow server staff to reset variables}
		{if;{isstaff;{userid}};==;true;{set;~canResetVariables;true};{set;~canResetVariables;false}}
	}

	{if;{get;~canResetVariables};==;true;
		{//;Reset variables}
		{set;_lastBombTime-{args;0};}
		{output;‚úÖ  |  Done, {usermention}! The variables for {exec;person;{args;0}} have been reset.};
		{output;‚ùå  |  Sorry {usermention}, but you don't have permission to do this!}
	}
}

{//;Help output, generated by flag -h}
{if;{flagset;h};==;true;
	{output;
		{embed;
			{embedbuild;
			title:üí£ Timebomb Help;
			description:**__Using the command__**{newline;2}{//;
        	}Type ``b!{commandname} <username / user ID / user mention>`` to bomb the selected user.{newline}{//;
        	}The bot will respond by mentioning the target user and adding three emoji reactions to the message: a red, white, and blue circle.{newline}{//;
            }The bot will then DM you the correct wire colour, just for fun!{newline;2}{//;
        	}**__Cutting the wire__**{newline;2}{//;
        	}Click on one of the coloured emoji reactions the bot presents to you to cut the wire. Only the bombed user can cut the wire{semi} other users cannot cut it for them.{newline;2}{//;
            }**__Outcome__**{newline;2}{//;
            }‚Ä¢ If the target user cuts the correct wire, they will be saved. If not, the bomb will go off.{newline}{//;
            }‚Ä¢ If the target user doesn't respond within one minute, the bomb will go off.{newline}{//;
            }‚Ä¢ If the bomb goes off, the target user will be informed of the correct wire colour that they should've picked.{newline;2}{//;
        	}**__Can't bomb someone?__**{newline;2}{//;
        	}You cannot bomb other bots, nor can you bomb yourself. You also cannot bomb someone if you have already bombed someone within the past minute.{newline;2}{//;
        	}**__Flags__**{newline;2}{//;
        	}``-d`` ‚Äì Enables debug output. For debugging purposes only.{newline}{//;
        	}``-h`` ‚Äì Displays this help output.{newline}{//;
            }``-q`` ‚Äì Quiet mode. Silences the DM the bot sends to the bombing user.{newline}{//;
        	}``-r`` ‚Äì Resets the variables for the command. For debugging purposes only.{newline;2}{//;
			}**__Found a bug?__**{newline;2}{//;
			}Join the [blargbot support server](https://discord.gg/015GVxZxI8rtlJgXF) and find me, {exec;person;249223024962830338}, there. I'll do my best to help you out.;
			color:e09040;
			footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version 1.3
			}
		}
	}
}
  
{//;Check to see if the user left the command blank}
{if;>;{argslength};0;

	{//;Check to see if the user has ever bombed anyone in the past}
	{if;{length;{get;_lastBombTime-{userid}}};>;0;
		{//;If true, take the current Unix time and subtract it from the last bomb time}
		{set;~currentTimeMinusLastTime-{userid};
			{math;-;{time;X};{get;_lastBombTime-{userid}}}
		}
		{//;If last bomb time was <= 60 seconds ago, do not allow user to bomb. If last bomb time was > 60 seconds ago, allow user to bomb.}
    	{if;{get;~currentTimeMinusLastTime-{userid}};>;60;{set;~canBombUser;true};{set;~canBombUser;false}};
      
		{//;If user has never bombed anyone on this server, ever, allow them to bomb}
		{set;~canBombUser;true}
	}
  
	{//;Check to see if the bomber has bombed someone in the past 60 seconds based on the check above}
	{if;{get;~canBombUser};==;true;

		{//;Get the victim's user ID}
		{set;~victim;{userid;{args};quiet}}
		{//;Check to see if the victim is in the server or if their user ID is valid}
		{if;{get;~victim};==;;
		‚ùå  |  Oops, I couldn't find the specified user in this server. Please specify a valid username or user ID.;
  
			{//;If user tried to blow up blargbot}
			{if;{get;~victim};==;134133271750639616;‚ùå  |  You thought you could trick me into bombing myself, {usermention}?!;
				{//;If user tried to blow themselves up}
				{if;{get;~victim};==;{userid};‚ùå  |  Please, {usermention}! Pick a friend if you have to!;
					{//;If user tried to blow up a bot}
					{if;{userisbot;{get;~victim}};==;true;‚ùå  |  I'm not bombing one of my own kin, {usermention}! Pick a *human* to blow up.;

						{//;Else, let the show continue}
                  
						{//;Set the Unix timestamp of the bombing message}
						{set;!_lastBombTime-{userid};
							{messagetime;
								{messageid};X
							}
						}

						{//;Set the wire color}
						{set;~wireColor;{randchoose; üî¥ ; ‚ö™ ; üîµ }}
                  
						{//;Get the text version of the color}
						{set;~wireColorText;
							{switch;{get;~wireColor};
								üî¥ ; red;
								‚ö™ ; white;
								üîµ ; blue;
								‚ùå **FATAL ERROR**: No wire color was set!!!
							}
						}

						{//;Sends the first message to the channel the command was used in}
						{set;~msgid;{send;{channelid};üí£  |  {randchoose;
							„Å≠, {usermention;{get;~victim}}, there's a stowaway in your pants! It's a bomb on a one minute timer. There are three wires: red, white, and blue. Click on the colour emoji below to cut that wire and try to defuse the bomb!;
							Hey {usermention;{get;~victim}}, someone shoved a bomb down your pants! There's one minute to go on the fuse, and there are three wires: red, white, and blue. Quick, click on the colour emoji below to tell me which wire to cut!;
							Watch out, {usermention;{get;~victim}}! There's a bomb strapped to your undies! The timer is set for one minute, and there are three wires: red, white, and blue. Click on the colour emoji below to cut that wire.;
							*Achtung!* There's a bomb down your pants, {usermention;{get;~victim}}! There's a one minute fuse on it, and there appears to be three wires: red, white, and blue. Given where it is, you should let me cut one of the wires for you{semi} click on the colour emoji below to tell me which one to cut!;
							Hey {usermention;{get;~victim}}, I saw someone put a bomb down your pants. One minute fuse, three wires: red, white, and blue. Time is running out! Click on the colour emoji below to cut that wire.
						}}}

						{//; Add the corresponding reactions to the message}
						{reactadd;{get;~msgid}; üî¥ ; ‚ö™ ; üîµ }
                  
						{//;DMs the initiating user the colour of the correct wire, unless they set flag -q}
						{if;{flagset;q};==;true;{//;Don't send DMs - user has set quiet mode};{dm;{userid};Hey, don't tell {usernick;{get;~victim}}, but it's the {get;~wireColorText} wire.}}
                  
						{//; Wait until user reacts one of the emojis provided}
						{void;
							{waitreaction;{get;~msgid};{userid;{get;~victim}};üî¥ ‚ö™ üîµ ;{set;~reaction;{reaction}}
							}
						}

						{//; Get the content of the reaction, then display either a success or fail message}
						{switch;{get;~reaction};
							üî¥ ; {//; User reacted red}
								{if;{get;~reaction};contains;{get;~wireColor};‚úÖ  |  {execcc;bomb-successmsg};üí•  |  {execcc;bomb-failmsg} (You should have picked the {get;~wireColorText} wire) };
							‚ö™  ; {//; User reacted white}
								{if;{get;~reaction};contains;{get;~wireColor};‚úÖ  |  {execcc;bomb-successmsg};üí•  |  {execcc;bomb-failmsg} (You should have picked the {get;~wireColorText} wire) };
							üîµ  ; {//; User reacted blue}
								{if;{get;~reaction};contains;{get;~wireColor};‚úÖ  |  {execcc;bomb-successmsg};üí•  |  {execcc;bomb-failmsg} (You should have picked the {get;~wireColorText} wire) };
							{//;User didn't react in time}üí•  |  {randchoose;
								Oh come on, {usermention;{get;~victim}}, you could've at least picked something! Well, now you're dead. You see that? Guts everywhere.;
								Too slow, {usermention;{get;~victim}}. You were a sitting duck waiting for the bomb to go off. Try to save yourself next time!;
								Well, it looks like {usermention;{get;~victim}} failed to pick a wire in time. Their indecisveness sure paid off. With their death!;
								Please, {usermention;{get;~victim}}, you could've at least tried! Oh well. As the saying goes: you'll always lose if you don't try.;
								Hello? Earth to {usermention;{get;~victim}}? You were just bombed! Next time, you should tell me which wire to cut to try to save yourself from annihilation.}{//;Display the wire they should've cut after the fail message} (You should have picked the {get;~wireColorText} wire)
						}
					}{//;End robot genocide check}
				}{//;End suicide check}
			}{//;End blargy immunity check}
		}{//;End victim user ID check}
    ;{//;If user has bombed someone recently}{set;~timeLeftToBomb;{math;-;60;{get;~currentTimeMinusLastTime-{userid}}}}‚ùå  |  Hey {usermention}, it looks like you've already bombed someone in the past minute. Please wait {if;{get;~timeLeftToBomb};>;1;another {get;~timeLeftToBomb} seconds;just a few more seconds} before trying again.
	}{//;End bomb throttling check}
  
{//;If command was left empty}
;{embed;
  {embedbuild;
	title:üí£ Tick, tick... BOOM!;
	description:Type ``b!{commandname} <username>`` to throw a bomb at someone! They have one minute to cut the correct wire or they will be blown to smithereens! Mentions will work, as will user IDs.{newline;2}{//;
    }**Found a bug?**{newline;2}{//;
    }Join the [blargbot support server](https://discord.gg/015GVxZxI8rtlJgXF) and find me, {exec;person;249223024962830338}, there. I'll do my best to help you out.;
	color:e09040;
    footer.text:Command created by {exec;person;249223024962830338} and is licensed under a CC BY-SA license ‚Ä¢ Version 1.3
  }
}
}{//;End empty command check}

{//;DEBUG MODE - set with flag -d}
{if;{flagset;d};==;true;**__Debugging information__**:
  
**Variables**
~currentTimeMinusLastTime-{userid}: {get;~currentTimeMinusLastTime-{userid}}
_lastBombTime-{userid}: {get;_lastBombTime-{userid}}
~canBombUser: {get;~canBombUser}
~timeLeftToBomb: {get;~timeLeftToBomb}
~victim: {get;~victim}
~wireColor: {get;~wireColor}
~wireColorText: {get;~wireColorText}
~msgid: {get;~msgid}
~reaction: {get;~reaction}
  
**Command info**
isstaff: {isstaff}
commandname: {commandname}
messagesender: {messagesender} ({exec;person;{messagesender}})
iscc: {iscc}
}
